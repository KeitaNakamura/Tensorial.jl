"""
`Quaternion` represents ``q_w + q_x \\bm{i} + q_y \\bm{j} + q_z \\bm{k}``.
The salar part and vector part can be accessed by `q.scalar` and `q.vector`, respectively.

# Examples
```jldoctest
julia> Quaternion(1,2,3,4)
1 + 2ùôû + 3ùôü + 4ùô†

julia> Quaternion(1)
1 + 0ùôû + 0ùôü + 0ùô†

julia> Quaternion(Vec(1,2,3))
0 + 1ùôû + 2ùôü + 3ùô†
```

See also [`quaternion`](@ref).

!!! note

    `Quaternion` is experimental and could change or disappear in future versions of Tensorial.
"""
struct Quaternion{T} <: Number
    data::NTuple{4, T}
    function Quaternion{T}(data::NTuple{4, Real}) where {T}
        new{T}(convert_ntuple(T, data))
    end
end

@inline Quaternion(data::NTuple{4, Any}) = Quaternion{promote_ntuple_eltype(data)}(data)
@inline (::Type{T})(data::Vararg{Any}) where {T <: Quaternion} = T(data)

# Quaternion <-> Vec
@inline Quaternion{T}(r::Real, v::Vec{3}) where {T} = @inbounds Quaternion{T}(r, v[1], v[2], v[3])
@inline Quaternion{T}(r::Real, v::Vec{2}) where {T} = @inbounds Quaternion{T}(r, v[1], v[2], zero(eltype(v)))
@inline Quaternion{T}(r::Real, v::Vec{1}) where {T} = @inbounds Quaternion{T}(r, v[1], zero(eltype(v)), zero(eltype(v)))
@inline Quaternion{T}(v::Vec{4}) where {T} = Quaternion{T}(Tuple(v))
for dim in 1:3
    @eval @inline Quaternion{T}(v::Vec{$dim}) where {T} = Quaternion{T}(zero(eltype(v)), v)
end
@inline Quaternion(v::Vec) = Quaternion{eltype(v)}(v)
@inline Quaternion(r::Real, v::Vec) = Quaternion{promote_type(typeof(r), eltype(v))}(r, v)
@inline Vec(q::Quaternion) = Vec(Tuple(q))

@inline Quaternion(x::Real) = (z = zero(x); Quaternion(x, z, z, z))

Base.Tuple(q::Quaternion) = getfield(q, :data)

@inline function Base.getproperty(q::Quaternion, name::Symbol)
    name == :scalar && return @inbounds q[1]
    name == :vector && return @inbounds Vec(q[2], q[3], q[4])
    name == :w && return @inbounds q[1]
    name == :x && return @inbounds q[2]
    name == :y && return @inbounds q[3]
    name == :z && return @inbounds q[4]
    getfield(q, name)
end

Base.propertynames(q::Quaternion) = (:scalar, :vector, :w, :x, :y, :z, :data)

# conversion
Base.convert(::Type{Quaternion{T}}, x::Quaternion{T}) where {T} = x
Base.convert(::Type{Quaternion{T}}, x::Quaternion{U}) where {T, U} = Quaternion(map(T, Tuple(x)))
Base.convert(::Type{Quaternion{T}}, x::Real) where {T} = convert(Quaternion{T}, Quaternion(x))

# promotion
Base.promote_rule(::Type{Quaternion{T}}, ::Type{T}) where {T <: Real} = Quaternion{T}
Base.promote_rule(::Type{Quaternion{T}}, ::Type{U}) where {T <: Real, U <: Real} = Quaternion{promote_type(T, U)}
Base.promote_rule(::Type{Quaternion{T}}, ::Type{Quaternion{T}}) where {T <: Real} = Quaternion{T}
Base.promote_rule(::Type{Quaternion{T}}, ::Type{Quaternion{U}}) where {T <: Real, U <: Real} = Quaternion{promote_type(T, U)}

# used for `isapprox`
Base.real(q::Quaternion) = q.scalar
Base.isfinite(q::Quaternion) = prod(map(isfinite, Tuple(q)))

"""
    quaternion(Œ∏, x::Vec; [normalize = true, degree = false])

Construct `Quaternion` from angle `Œ∏` and axis `x`.
The constructed quaternion is normalized such as `norm(q) ‚âà 1` by default.

# Examples
```jldoctest
julia> q = quaternion(œÄ/4, Vec(0,0,1))
0.9238795325112867 + 0.0ùôû + 0.0ùôü + 0.3826834323650898ùô†

julia> v = rand(Vec{3})
3-element Vec{3, Float64}:
 0.5908446386657102
 0.7667970365022592
 0.5662374165061859

julia> (q * v / q).vector ‚âà rotmatz(œÄ/4) ‚ãÖ v
true
```
"""
function quaternion(::Type{T}, Œ∏::Real, x::Vec{3}; normalize::Bool = true, degree::Bool = false) where {T}
    if degree
        Œ∏ = deg2rad(Œ∏)
    end
    œï = Œ∏ / 2
    if normalize
        n = LinearAlgebra.normalize(x) * sin(œï)
    else
        n = x * sin(œï)
    end
    @inbounds Quaternion{T}(cos(œï), n)
end
quaternion(T::Type, Œ∏::Real, x::Vec{2}; normalize::Bool = true, degree::Bool = false) =
    @inbounds quaternion(T, Œ∏, Vec(x[1], x[2], 0); normalize, degree)
quaternion(Œ∏::Real, x::Vec; normalize::Bool = true, degree::Bool = false) =
    quaternion(promote_type(typeof(Œ∏), eltype(x)), Œ∏, x; normalize, degree)

Base.length(::Quaternion) = 4
Base.size(::Quaternion) = (4,)

@inline function Base.getindex(q::Quaternion, i::Int)
    @boundscheck 1 ‚â§ i ‚â§ 4 || throw(BoundsError(q, i))
    @inbounds Tuple(q)[i]
end

# quaternion vs quaternion
@inline Base.:-(q::Quaternion) = Quaternion(-Vec(q))
@inline Base.:+(q::Quaternion, p::Quaternion) = Quaternion(Vec(q) + Vec(p))
@inline Base.:-(q::Quaternion, p::Quaternion) = Quaternion(Vec(q) - Vec(p))
@inline Base.:/(q::Quaternion, p::Quaternion) = q * inv(p)
@inline function Base.:*(q::Quaternion, p::Quaternion)
    q‚ÇÅ, q‚ÇÇ, q‚ÇÉ, q‚ÇÑ = Tuple(q)
    A = @Mat [ q‚ÇÅ -q‚ÇÇ -q‚ÇÉ -q‚ÇÑ
               q‚ÇÇ  q‚ÇÅ -q‚ÇÑ  q‚ÇÉ
               q‚ÇÉ  q‚ÇÑ  q‚ÇÅ -q‚ÇÇ
               q‚ÇÑ -q‚ÇÉ  q‚ÇÇ  q‚ÇÅ ]
    Quaternion(A ‚ãÖ Vec(p))
end

# quaternion vs number
@inline Base.:*(a::Number, q::Quaternion) = Quaternion(a * Vec(q))
@inline Base.:*(q::Quaternion, a::Number) = Quaternion(Vec(q) * a)
@inline Base.:/(q::Quaternion, a::Number) = Quaternion(Vec(q) / a)

# quaternion vs vector
@inline Base.:*(q::Quaternion, v::Vec) = q * Quaternion(v)
@inline Base.:*(v::Vec, q::Quaternion) = Quaternion(v) * q
@inline Base.:/(v::Vec, q::Quaternion) = v * inv(q)

"""
    rotate(x::Vec, q::Quaternion)

Rotate `x` by quaternion `q`.

# Examples
```jldoctest
julia> v = Vec(1.0, 0.0, 0.0)
3-element Vec{3, Float64}:
 1.0
 0.0
 0.0

julia> rotate(v, quaternion(œÄ/4, Vec(0,0,1)))
3-element Vec{3, Float64}:
 0.7071067811865475
 0.7071067811865476
 0.0
```
"""
@inline rotate(v::Vec{3}, q::Quaternion) = (q * v / q).vector
@inline rotate(v::Vec{2}, q::Quaternion) = (v = (q * v / q).vector; @inbounds Vec(v[1], v[2]))

@inline Base.conj(q::Quaternion) = @inbounds Quaternion(q[1], -q[2], -q[3], -q[4])
@inline Base.abs2(q::Quaternion) = (v = Vec(q); dot(v, v))
@inline Base.abs(q::Quaternion) = sqrt(abs2(q))
@inline norm(q::Quaternion) = abs(q)
@inline inv(q::Quaternion) = conj(q) / abs2(q)

function Base.exp(q::Quaternion)
    v = q.vector
    norm_v = norm(v)
    if norm_v > 0
        n = v / norm_v
    else
        n = zero(v)
    end
    exp(q.scalar) * quaternion(2norm_v, n; normalize = false)
end

function Base.log(q::Quaternion)
    norm_q = norm(q)
    v = q.vector
    norm_v = norm(v)
    Quaternion(log(norm_q), Tuple(v/norm_v * acos(q.scalar/norm_q))...)
end

@inline normalize(q::Quaternion) = q / norm(q)

function rotmat_normalized(q::Quaternion)
    q‚ÇÅ, q‚ÇÇ, q‚ÇÉ, q‚ÇÑ = Tuple(q)
    q‚ÇÅ¬≤ = q‚ÇÅ * q‚ÇÅ
    q‚ÇÇ¬≤ = q‚ÇÇ * q‚ÇÇ
    q‚ÇÉ¬≤ = q‚ÇÉ * q‚ÇÉ
    q‚ÇÑ¬≤ = q‚ÇÑ * q‚ÇÑ
    q‚ÇÅq‚ÇÇ = q‚ÇÅ * q‚ÇÇ
    q‚ÇÇq‚ÇÉ = q‚ÇÇ * q‚ÇÉ
    q‚ÇÉq‚ÇÑ = q‚ÇÉ * q‚ÇÑ
    q‚ÇÅq‚ÇÉ = q‚ÇÅ * q‚ÇÉ
    q‚ÇÅq‚ÇÑ = q‚ÇÅ * q‚ÇÑ
    q‚ÇÇq‚ÇÑ = q‚ÇÇ * q‚ÇÑ
    @Mat [q‚ÇÅ¬≤+q‚ÇÇ¬≤-q‚ÇÉ¬≤-q‚ÇÑ¬≤ 2(q‚ÇÇq‚ÇÉ-q‚ÇÅq‚ÇÑ)    2(q‚ÇÇq‚ÇÑ+q‚ÇÅq‚ÇÉ)
          2(q‚ÇÇq‚ÇÉ+q‚ÇÅq‚ÇÑ)    q‚ÇÅ¬≤-q‚ÇÇ¬≤+q‚ÇÉ¬≤-q‚ÇÑ¬≤ 2(q‚ÇÉq‚ÇÑ-q‚ÇÅq‚ÇÇ)
          2(q‚ÇÇq‚ÇÑ-q‚ÇÅq‚ÇÉ)    2(q‚ÇÉq‚ÇÑ+q‚ÇÅq‚ÇÇ)    q‚ÇÅ¬≤-q‚ÇÇ¬≤-q‚ÇÉ¬≤+q‚ÇÑ¬≤]
end
@inline rotmat(q::Quaternion) = rotmat_normalized(normalize(q))

function Base.show(io::IO, q::Quaternion)
    pm(x) = x < 0 ? " - $(-x)" : " + $x"
    print(io, q[1], pm(q[2]), "ùôû", pm(q[3]), "ùôü", pm(q[4]), "ùô†")
end
